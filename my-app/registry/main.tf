terraform {
    required_providers {
        aws = {
            source  = "hashicorp/aws"
            version = "~>5.92"
        }
    }
    required_version = ">= 1.2"
}

provider "aws" {
    region = "eu-west-3"
}

data "aws_ami" "ubuntu" {
    most_recent = true
    owners      = ["099720109477"]

    filter {
        name   = "name"
        values = ["ubuntu/images/hvm-ssd-gp3/ubuntu-noble-24.04-amd64-server-*"]
    }
}

resource "tls_private_key" "pk" {
    algorithm = "RSA"
    rsa_bits  = 4096
}

resource "aws_key_pair" "generated_key" {
    key_name   = "registry-key-terraform"
    public_key = tls_private_key.pk.public_key_openssh
}

resource "local_file" "ssh_key" {
    filename        = "${path.module}/registry-key-terraform.pem"
    content         = tls_private_key.pk.private_key_pem
    file_permission = "0400"
}

resource "aws_security_group" "registry_sg" {
    name        = "registry-sg-simple"
    description = "Allow SSH, HTTP (UI), HTTPS (UI)"

    # SSH
    ingress {
        description = "SSH"
        from_port   = 22
        to_port     = 22
        protocol    = "tcp"
        cidr_blocks = ["0.0.0.0/0"]
    }

    # HTTP (optionnel si redirection HTTP -> HTTPS)
    ingress {
        description = "Registry UI HTTP"
        from_port   = 80
        to_port     = 80
        protocol    = "tcp"
        cidr_blocks = ["0.0.0.0/0"]
    }

    # HTTPS
    ingress {
        description = "Registry UI HTTPS"
        from_port   = 443
        to_port     = 443
        protocol    = "tcp"
        cidr_blocks = ["0.0.0.0/0"]
    }

    # Note : port 5000 fermé pour l'extérieur (pas de règle pour 5000)

    egress {
        from_port   = 0
        to_port     = 0
        protocol    = "-1"
        cidr_blocks = ["0.0.0.0/0"]
    }
}

resource "aws_instance" "registry_server" {
    ami           = data.aws_ami.ubuntu.id
    instance_type = "t3.micro"
    key_name      = aws_key_pair.generated_key.key_name

    vpc_security_group_ids = [aws_security_group.registry_sg.id]

    root_block_device {
        volume_size = 20
        volume_type = "gp3"
    }

    tags = {
        Name = "Terraform-Registry-Server"
    }
}

output "instance_ip" {
    value = aws_instance.registry_server.public_ip
}